TEXTJOIN("",TRUE,
LET(
stateset3,IF($F$18<>"",$F$18,"jJrsSjhFR:B.H>?1HoiFqY$Kbri"),
fullStr,IF($F$19<>"",$F$19,"u.;gA:SMYsUfAg.3kGFLJ"),
offsetStr,IF($F$18<>"",$F$18,"H4\7JGFk3.f(88LQoH_,si+"),
password,IF($F$20<>"",$F$20,"dTQ#}'&gS{s##e>=-1R"),

offsetNum,SUMPRODUCT(CODE(MID(offsetStr,ROW(INDIRECT("1:"&LEN(offsetStr))),1))),
baseOffset,MOD(offsetNum,89)+1,

maskMult,MOD(
SUMPRODUCT(CODE(MID(offsetStr,ROW(INDIRECT("1:"&LEN(offsetStr))),1)))+LEN(offsetStr),
MOD(LEN(offsetStr),SUMPRODUCT(CODE(MID(stateset3,ROW(INDIRECT("1:"&LEN(stateset3))),1))*LEN(stateset3)))+SUMPRODUCT(CODE(MID(stateset3,ROW(INDIRECT("1:"&LEN(stateset3))),1))*LEN(stateset3))
)+5,

maskOffset1,MOD(offsetNum+LEN(offsetStr),89)+1,
maskOffset2,MOD(offsetNum*maskMult,89)+1,
maskOffset3,MOD((offsetNum+maskMult)*LEN(offsetStr),89)+1,
maskOffset4,MOD(offsetNum*(maskMult+LEN(offsetStr)),89)+1,

passwordMask,MOD(SUMPRODUCT(CODE(MID(password,ROW(INDIRECT("1:"&LEN(password))),1)))*LEN(password),89),
totalMask,MOD(maskOffset1+maskOffset2+maskOffset3+maskOffset4+passwordMask,89),

restored,IF(AND($F$18="",$F$19="",$F$20=""),
fullStr,
LEFT(fullStr,LEN(fullStr)-2)&RIGHT(fullStr,1)
),

n,LEN(restored),
seq,SEQUENCE(n),

charMask,IF(ISODD(seq),
MOD(maskOffset1+maskOffset3+passwordMask,89),
MOD(maskOffset2+maskOffset4+passwordMask,89)
),

chars,MID(restored,seq,1),

compRows,MAP(chars,LAMBDA(c,
XLOOKUP(TRUE,EXACT(c,$A$2:$A$90),ROW($A$2:$A$90)-ROW($A$2)+1)
)),
cipherRows,MOD(compRows-baseOffset-totalMask-MOD(seq*3,89)-seq+89*4,89)+1,
cipherTokens,INDEX($B$2:$B$90,cipherRows),

cipherTokenRows,MAP(cipherTokens,LAMBDA(t,
XLOOKUP(TRUE,EXACT(t,$B$2:$B$90),ROW($B$2:$B$90)-ROW($B$2)+1)
)),
charRows,
MOD(
cipherTokenRows
-baseOffset
-totalMask
-MOD(seq*3,89)
-seq
+IF(AND($F$18="",$F$19="",$F$20=""),seq*7,89*4),
89)+1,

INDEX($C$2:$C$90,charRows)
)
)
